task wrapper(type: Wrapper) {
    gradleVersion = '2.7'
}
buildscript {
    repositories {
        maven { url 'http://repo.spring.io/plugins-release' }
	jcenter()
    }
    dependencies {
        classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.6'
    }
}

apply plugin: "java"
apply plugin: 'propdeps'

repositories {
    mavenCentral()
    maven {
	url  "http://dl.bintray.com/white-oak/maven" 
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

sourceSets.main.java.srcDirs = [ "src/" ]

dependencies {   
    compile 'net.sf.trove4j:trove4j:3.0.3'
    compile 'me.whiteoak.minlog:oak-minlog:1.0.5'
    provided 'org.projectlombok:lombok:1.16.6'
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'com.google.guava:guava:18.0'
}

ext{
    buildNumberIncremented = false
    mBuildNumber = 1
    mVersion = "1.0.0"
}

task run(dependsOn: classes, type: JavaExec) {
    main = "me.oak.imgcolor.Main"
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    ignoreExitValue = true
    enableAssertions = true
}

task debug(dependsOn: classes, type: JavaExec) {
    main = "me.oak.imgcolor.Main"
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    ignoreExitValue = true
    enableAssertions = true
    debug = true
}

task versionCodeUpdate {
    def different = fileTree('src'){
	include '**/*.java'
	exclude '**/VersionCode.java'
    }
    def fileV = file("./version.code")
    def fileVC = file("./src/me/oak/imgcolor/VersionCode.java")
    inputs.files  different
    outputs.file  fileVC

    if(fileV.exists()){
	fileV.withReader('UTF-8'){fr ->
	    project.ext.mBuildNumber = Integer.parseInt(fr.readLine())
	    project.ext.mVersion = fr.readLine()
	}
    }
    jar.baseName = 'imageDOER'
    jar.version = "${mVersion}-b${mBuildNumber}"
    doLast{
	if(!project.ext.buildNumberIncremented){
	    project.ext.mBuildNumber++
	    project.ext.buildNumberIncremented = true
	    println(":${project.name}:versionCodeUpdate - current build number is ${mBuildNumber}")
	}else{
	    println(":${project.name}:versionCodeUpdate - build number has already been incremented in this build -- check your build pipeline.")
	}
	overwriteVersionCodeFile(mBuildNumber, mVersion, fileV)
	createVersionCodeJava(mBuildNumber, mVersion, fileVC)
	jar.baseName = 'imageDOER'
	jar.version = "${mVersion}-b${mBuildNumber}"
    }
}
void createVersionCodeJava(int versionNum, String versionString, File fileVC){
    if(fileVC.exists()) fileVC.delete()
    def versionCodeJava = """\n\
package me.oak.imgcolor;

public final class VersionCode {

    public static final int BUILD_NUMBER = ${versionNum};
    public static final String VERSION = "${versionString}";

    public static String getCode() {
	return VERSION + "-b" + BUILD_NUMBER;
    }
}
"""
    fileVC.text = versionCodeJava
}

void overwriteVersionCodeFile(int num, String version, File file){
    if(file.exists()){
	file.delete()
    }
    file.text = "${num}\n${version}"
}

compileJava.dependsOn versionCodeUpdate